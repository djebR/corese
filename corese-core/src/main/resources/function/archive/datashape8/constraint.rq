#
# SHACL Interpreter 
#
# Olivier Corby - Wimmics Inria I3S - 2016-2019
#

prefix sh:  <http://www.w3.org/ns/shacl#> 
prefix xsh: <http://www.w3.org/ns/shacl#> 
prefix msh: <http://ns.inria.fr/shacl/>

function sh:constraint(sh, vis, nodeList) {
    let (b1 = sh:constraint1 (sh, vis, nodeList),
         b2 = sh:constraint2 (sh, vis, nodeList),
         b4 = sh:nodeextension(sh, vis, nodeList),
         b5 = sh:cstsparql   (sh:shaclGraph(), sh, vis, nodeList)
         ) {
        return (b1 && b2 && b4 && b5)
    }
}

# shape sh has several operators with value, ex: oper=sh:datatype value=xsd:string
# operator defined as a function (in operator.rq) with same name
# operator executed as funcall(oper, value)
# list of (oper value) is computed once for each shape sh and stored in a table 
# sh:getConstraint retrieve the list (oper value)
# nodeList is either a list of nodes or a single node
# sh:report generates a validation report in case of error
#
function sh:constraint1 (sh, vis, nodeList) {
    let (res = true) {
        for ((oper value) in sh:getShapeConstraint(sh:cstgeneric1, sh)) {  
            for (node in dt:list(nodeList)) {    
                let (suc = coalesce(funcall(oper, node, value), false)) {
                        sh:report(oper, sh, if (oper = sh:node, value, st:null),  node, st:null, node, suc, vis) ;
                        if (suc, true, set(res = false))
                    }
            }
        } ;
        return (res)
    }
}


# oper = xsh:function
function sh:nodeextension (sh, vis, nodeList) {
    let (res = true) {
        for ((oper name value) in sh:getShapeConstraint(sh:nodeextension, sh)) {  
            for (node in dt:list(nodeList)) {  
                let (suc = coalesce(
                        funcall(name, node, value),
                        false)) {
                        sh:report(oper, sh, name, node, st:null, node, suc, vis) ;
                        if (suc, true, set(res = false))
                    }
            }
        } ;
        return (res)
    }
}


function sh:constraint2 (sh, vis, nodeList) {
    let (res = true) {
        for ((oper value) in sh:getShapeConstraint(sh:cstgeneric2, sh)) {  
            for (node in dt:list(nodeList)) {    
                let (suc = coalesce(funcall(oper, sh, vis, node, value), false)) {
                    if (suc, true, set(res = false))
                }
            }
        } ;
        return (res)
    }
}


function sh:getGenericShape(shape, sh, list) {
    let (select ?shape ?sh ?list 
        (aggregate(xt:list(?oper, ?arg)) as ?res) 
        where {
        graph ?shape {  
            values ?list {UNDEF}
            values ?oper { unnest(?list) }
            ?sh ?oper ?val 
            optional { ?sh sh:flags ?flag }
            bind (
                if (?oper = sh:pattern,   coalesce(xt:list(?val, ?flag), xt:list(?val)),
                if (?oper = sh:patternIn, sh:getValueList(?val),
                ?val))            
            as ?arg)
        }
    } ) {
        return(res)
    }
}





# xsh:function [ us:fun (h:age 50) ]
# xsh:function [ us:fun () ]
# xsh:function us:fun
# xsh:evaluate (rq:gt h:age (rq:mult 2 h:shoesize)) -> xsh:function [ ... ]
# list = (sh:evaluate sh:compute ...)
#
function sh:getShapeExtension(shacl, sh, list) {
    let (select ?shacl ?sh ?list
            (aggregate(
                xt:list(xsh:function, ?name, ?arg, ?ternary)
            ) as ?res) 
        where {
            graph ?shacl {  
                {   values ?list {UNDEF}
                    ?sh ?name ?value 
                    filter xt:member(?name, ?list)
                }
                union { values ?list {UNDEF}
                    ?sh  xsh:function ?fun optional { ?fun ?oper ?param } 
                    bind (coalesce(?oper, ?fun) as ?name)
                    bind (coalesce(?param, xt:list()) as ?value)
                }
                # in this context, sh:node is not a function, it is a std SHACL constraint
                filter (?name != sh:node) 
                bind (sh:compile(?name, sh:getValueList(?value)) as ?arg)
                # used in path.rq
                bind (xt:isFunction(?name, 2) as ?binary)
                bind (xt:isFunction(?name, 3) as ?ternary)
                filter (?binary || ?ternary) # function is defined
            }
        } ) {
        return(res)
    }
}



# (fun x y) -> (fun (x y))
function sh:compile(name, exp) {
    if (name in (xsh:evaluate, xsh:compute), sh:compile(exp), exp)
}


# (fun x1 x2) -> (fun (x1 x2))
#
function sh:compile(exp) {
    if (isExtension(exp)) {
        if (xt:size(exp) = 0) {
            return (exp)
        }
        else {
            let ((oper | param) = exp) {
                xt:list(oper, maplist(sh:compile, param))
            }
        }
    }
    else {
        exp
    }
}

function sh:getValueList(value) {
    if (isBlank(value)) {
       coalesce(sh:reclist(value), value)
    }
    else {
        return(value)
    }
}

function sh:reclist(l) {
  let (select ?l 
       (aggregate (if (?b, sh:reclist(?e), 
                   if (?e = rdf:nil, xt:list(), ?e))) as ?list) 
        where {
            ?l rdf:rest*/rdf:first ?e
            bind (exists { ?e rdf:rest ?a } as ?b)
        } ) {
    return (list)
  }
}


function sh:cstgeneric1(shape, sh) {
    sh:getGenericShape(shape, sh, sh:getShape(sh:node1))
}

function sh:cstgeneric2(shape, sh) {
    sh:getGenericShape(shape, sh, sh:getShape(sh:node2))
}

function sh:nodeextension(shape, sh) {
    sh:getShapeExtension(shape, sh, sh:getShape(sh:nodeextension))
}


function sh:class (sh, vis, s, ?c) {
    let (suc = exists { ?s rdf:type/rdfs:subClassOf* ?c } ) {
        sh:report(sh:class, sh, s, rdf:type, s, suc, vis) ;
        return (suc)
    }
}


function sh:disjoint (sh, vis, s, p) {
    let (l = sh:ppath(s, p), suc = ! xt:member(s, l)) {
        sh:report(sh:disjoint, sh, s, p, s, suc, vis) ;
        return (suc)
    }
}

function sh:equals (sh, vis, s, p) {
    let (l  = sh:ppath(s, p),
         b1 = sh:equals1(sh, vis, s, p, l),
         b2 = sh:equals2(sh, vis, s, p, l)) {
            return (b1 && b2)
    }
}


function sh:equals1 (sh, vis, s, p, l) {
    let (suc = xt:member(s, l)) {
        sh:report(sh:equals, sh, s, p, s, suc, vis);
        return (suc)
    }
}

function sh:equals2 (sh, vis, s, p, l) {
    let (res = true) {
        for (m in l) {
            let (suc = s = m) {
                sh:report(sh:equals, sh, s, p, m, suc, vis);
                set (res = res && suc)
            }
        } ;
        return (res)
    }
}










