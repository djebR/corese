#
# SHACL Interpreter 
#
# Olivier Corby - Wimmics Inria I3S - 2016-2019
#

prefix sh:  <http://www.w3.org/ns/shacl#> 
prefix xsh: <http://www.w3.org/ns/shacl#> 
prefix msh: <http://ns.inria.fr/shacl/>

@import <sparql.rq>


#
# shape: shacl graph
# sh: current shape
# vis: true if report is needed
# nodeList:  list of target nodes of shape sh
# path: path expression
# present=true: fail when path is empty, use case: qualified value shape
# 
function sh:path(sh, vis, nodeList, path, present) {
    if (sh:trace(), xt:print("path:", nodeList, path), true);
   let (b1 = sh:path123(sh, vis, nodeList, path, present), 
       #b2 = sh:path2(sh, vis, nodeList, path), 
       #b3 = sh:path3(sh, vis, nodeList, path),
       b5 = sh:pathextension(sh, vis, nodeList, path),
       b6 =      sh:sparql(sh, vis, nodeList, path)
       ) {
       return (b1 &&  b5 && b6)
    }
}




#
# nodeList: target node list 
# p: the path
# oper=sh:nodeKind ; val=sh:IRI
# present = true: fail when path is empty
#

function sh:path123(sh, vis, nodeList, p, present) {
    let (res = true, 
        cstList1 = sh:getShapeConstraint(sh:path1, sh),
        cstList2 = sh:getShapeConstraint(sh:path2, sh),
        cstList3 = sh:getShapeConstraint(sh:path3, sh),
        b1 = xt:size(cstList1) > 0,
        b2 = xt:size(cstList2) > 0,
        b3 = xt:size(cstList3) > 0) {
        if (b1 || b2 || b3) {
            #xt:print("path:", nodeList, p);
            for (s in dt:list(nodeList)) {
                let (targetList = sh:ppath(s, p)) {
                
                    if (b1) {
                        # special case for qualified value shape
                        if (present && xt:size(targetList) = 0, return(false), true);
                        for (o in targetList) {
                            for ((oper val) in cstList1) {
                                let (suc = coalesce(funcall(oper, o, val), false)) {
                                        if (oper in (sh:property), true, 
                                                sh:report(oper, sh, if (oper = sh:node, val, st:null), s, p, o, suc, vis)) ;
                                        if (! suc) {
                                            set(res = false) 
                                        }
                                    }
                                }
                        }
                    } ;
                    
                    if (b2) {
                        # sh:minCount ...
                        for ((oper val) in cstList2) {
                            let (suc = coalesce(funcall(oper, sh, vis, s, p, val, targetList),   false)) {
                                if (! suc) {
                                    set(res = false) 
                                }
                            }
                        }
                    } ;
                    
                    if (b3) {
                        for ((oper val disjoint amin amax) in cstList3) {
                            let (suc = coalesce(                    
                                    sh:qualifiedValueShape(sh, vis, s, p, val, disjoint, amin, amax, targetList) , 
                                    false) ) {
                                if (! suc) {
                                    set(res = false) 
                                }
                            }
                        }
                    }
                    
                }
            }
        } ;
        return (res)
    }
}


# qualified value shape
function sh:path3(sh, vis, nodeList, p) {
    let (res = true) {
        for ((oper val disjoint amin amax) in sh:getShapeConstraint(sh:path3, sh)) {
            for (s in dt:list(nodeList)) {
                let (suc = coalesce(                    
                        sh:qualifiedValueShape(sh, vis, s, p, val, disjoint, amin, amax) , 
                        false) ) {
                    if (! suc) {
                        set(res = false) 
                    }
                }
            }
        } ;
        return (res)
    }
}

#
# nodeList: target node list 
# p: the path
# oper=sh:nodeKind ; val=sh:IRI
# present = true: fail when path is empty
#
function sh:path1(sh, vis, nodeList, p, present) {
    let (res = true, cstList = sh:getShapeConstraint(sh:path1, sh)) {
        if (xt:size(cstList) > 0) {
            #xt:print("path1:", nodeList, p);
            for (s in dt:list(nodeList)) {
                let (targetList = sh:ppath(s, p)) {
                    if (present && xt:size(targetList) = 0, return(false), true);
                    for (o in targetList) {
                        for ((oper val) in cstList) {
                            let (suc = coalesce(
                                        funcall(oper, o, val), 
                                        false)) {
                                    if (oper in (sh:property), true, 
                                            sh:report(oper, sh, if (oper = sh:node, val, st:null), s, p, o, suc, vis)) ;
                                    if (! suc) {
                                        set(res = false) 
                                    }
                                }
                            }
                    }
                }
            }
        } ;
        return (res)
    }
}

function sh:path2(sh, vis, nodeList, p) {
    let (res = true, cstList2 = sh:getShapeConstraint(sh:path2, sh)) {
        if (xt:size(cstList2) > 0) {
            #xt:print("path2:", nodeList, p);
            for (s in dt:list(nodeList)) {
                let (targetList = sh:ppath(s, p)) {
                    for ((oper val) in cstList2) {
                        let (suc = coalesce(funcall(oper, sh, vis, s, p, val, targetList),   false)) {
                            if (! suc) {
                                set(res = false) 
                            }
                        }
                    }
                }
            }
        } ;
        return (res)
    }
}


# xsh:function [ name (value) ]
function sh:pathextension(sh, vis, nodeList, p) {
    let (res = true, cstList = sh:getShapeConstraint(sh:pathextension, sh)) {
        if (xt:size(cstList) > 0) {
            for (s in dt:list(nodeList)) {
                let (targetList = sh:ppath(s, p)) {
                    for ((oper name value ternary) in cstList) {
                        if (name = sh:display) {
                            funcall(name, s, targetList, value)
                        }
                        else {
                            for (o in targetList) {
                                let (suc = coalesce(
                                    if (ternary, 
                                        funcall(name, s, o, value), 
                                        funcall(name, o, value)), 
                                        false)) {
                                        sh:report(oper, sh, name, s, p, o, suc, vis) ;
                                        if (! suc) {
                                            set(res = false) 
                                    }
                                }
                            }
                        }
                    }
                }
            }
        } ;
        return (res)
    }
}











# *******************************************


#
# Retrieve properties of constraint sh using a query, do it once
# they are stored as a list in a map by sh:getConstraint defined in core.rq
#

function sh:getPathShape(shape, sh, shapeList) {
    let (select ?shape ?sh ?shapeList 
    (aggregate(xt:list(?oper, ?arg)) as ?list) 
    where {
            graph ?shape { 
                values ?shapeList {UNDEF}
                values ?oper { unnest(?shapeList) }
                ?sh ?oper ?val 
                optional { ?sh sh:flags ?flag }
                bind (
                    if (?oper = sh:pattern,   coalesce(xt:list(?val, ?flag), xt:list(?val)),
                    if (?oper = sh:patternIn, sh:getValueList(?val),
                    ?val))
                as ?arg)
                filter if (?oper = sh:uniqueLang, sameTerm(?val, true), true)
            }
        } ) {
        return(list)
    }
}


function sh:pathextension(shape, sh) {
    sh:getShapeExtension(shape, sh, sh:getShape(sh:pathextension))
}
        
function sh:path1(shape, sh) {
    sh:getPathShape(shape, sh, sh:getShape(sh:path1))
}

function sh:path2(shape, sh) {
    sh:getPathShape(shape, sh, sh:getShape(sh:path2))
}

function sh:path3(shape, sh) {
    let (select ?shape ?sh 
        (aggregate(xt:list(sh:qualifiedValueShape, ?qsh, 
            coalesce(disjoint, false), coalesce(?min, 0), coalesce(?max, -1))) as ?list) 
        where {
            graph ?shape {  
                ?sh sh:qualifiedValueShape ?qsh
                optional { ?sh sh:qualifiedMinCount ?min }
                optional { ?sh sh:qualifiedMaxCount ?max }
                optional { ?sh sh:qualifiedValueShapesDisjoint ?disjoint }
            }
        } ) {
        return(list)
    }
}


#
# called when start
# within xt:focus(?shape, sh:sibling())
# record sibling qualifiedValueShape in a table
#
function sh:sibling() {
    for (select  ?qsh ?list where {
            select ?qsh (aggregate(xt:list(?sibling, ?path)) as ?list) 
            where {
                select distinct ?qsh ?sibling ?path
                where {
                    ?root sh:property [ sh:qualifiedValueShape ?qsh ];
                          sh:property [ sh:qualifiedValueShape ?sibling ; sh:path ?path ]  
                    filter (?sibling != ?qsh)
                }
            }
            group by ?qsh
        } ) {
            if (bound(?qsh), sh:setConstraint(sh:sibling, ?qsh, ?list), true) 
    } ;
    return (true)
}


# ************** operators

# property shape
#
function sh:qualifiedValueShape (sh, vis, s, p, qsh, disjoint, amin, amax, nodeList) {
        #xt:print("qualif1:", s, nodeList);
        # list = path nodes that match qualified shape
        let (list = mapfindlist(sh:qualifiedShape, nodeList,  qsh),
            qlist = if (coalesce(disjoint, false), 
                # remove elements that are in sibling shapes
                mapfindlist(sh:disjoint, list,  qsh, s), list),
            suc1 = xt:size(qlist) >= amin, 
            suc2 = if (amax = -1, true, xt:size(qlist) <= amax)) {
            
            #xt:print("qualif3:", s, qlist);
            sh:report(sh:qualifiedMinCount, sh, s, p, s, suc1, vis) ;
            sh:report(sh:qualifiedMaxCount, sh, s, p, s, suc2, vis) ;
            return (suc1 && suc2)
        }
}



#
# qsh = [ sh:path us:genre ; sh:hasValue us:female ]
# check shape qsh on subject
# absence of us:genre return false because we will count the number of nodes
# that *verify* the shape
#
function xsd:boolean sh:qualifiedShape(subject,  qsh){
    #xt:print("qualif2:", subject, xt:turtle(qsh));
    sh:core(sh:shaclGraph(), qsh, false, subject, true)
}


#
# Return the list of value nodes that match qualified shape qsh 
#
function sh:qualified(qsh, s, p){
    # nodes of path p
     let (list = sh:ppath(s, p)) {
        # nodes of path p that match qualified shape qsh
        mapfindlist(sh:qualifiedShape, list,  qsh)
     }
}



#
# test disjointness of subject ?s value ?o with ?qsh sibling shapes
# for each sibling shape ?sh, compute ?list of values and test ?o not in ?list
#
function sh:disjoint(o, qsh, s){
   for ((sh, path) in sh:getConstraint(sh:sibling, qsh)){
        if (xt:member(o, sh:qualified(sh, s, path))){
            return (false)
        }        
   } ;
   return (true)
}








function sh:lessThan(sh, vis, s, p, q, nodeList){
    sh:lessThanList(sh, vis, s, p, q, false, sh:lessThan, nodeList)
}

function sh:lessThanOrEquals(sh, vis, s, p, q, nodeList){
    sh:lessThanList(sh, vis, s, p, q, true, sh:lessThanOrEquals, nodeList)
}

function sh:lessThanList(sh, vis, s, p, q, leq, oper, nodeList){
    let (suc = true, targetList = xt:objects(s, q)) {
        for (o in nodeList) { #xt:objects(s, p)) {
            for (v in targetList) {
                let (res = coalesce(if (leq, o <= v, o < v), false)) {
                    sh:report(oper, sh, s, p, o, res, vis) ;
                    if (! res) {
                        set(suc = false)
                    } 
                }
            }
        } ;
        return (suc)
    }
}


function sh:uniqueLang (sh, vis, s, p, v, nodeList){
    let (suc = true, amap = xt:map(),
         nodeLang = mapfindlist(lambda(value) { coalesce(lang(value) != "", false) }, nodeList)) {
         
         for (value in nodeLang) {
            let (lan = lang(value)) {
                xt:set(amap, lan, coalesce(xt:get(amap, lan), 0) + 1)
            }
         } ;
         
         for ((lan value) in amap) {
            let (res = value = 1) {
                if (res, true, set(suc = false));
                sh:report(sh:uniqueLang, sh, s, p, lan, res, vis)
            }
         } ;
         
        return (suc)
    }
}



function sh:hasValue (sh, vis, s, p, v, nodeList) {
    let (suc = sh:hasValueList(v, nodeList)) {
        sh:report(sh:hasValue, sh, s, p, v, suc, vis) ;
        return (suc)
    }
}

function sh:hasValueList (object, list) {
    for (value in list) {
        if (coalesce(object = value, false), return(true), true)
    } ;
    return (false)
}


function sh:minCount (sh, vis, s, p, m, nodeList) {
    let (val = xt:size(nodeList),
         suc = val >= m) { 
        sh:report(sh:minCount, sh, s, p, val, suc, vis);
        return(suc)
    }
}

function sh:maxCount (sh, vis, s, p, m, nodeList) {
    let (val = xt:size(nodeList),
         suc = val <= m) { 
        sh:report(sh:maxCount, sh, s, p, val, suc, vis);
        return(suc)
    }
}





function sh:disjoint (sh, vis, s, p, q, nodeList) {
    let (res = true, 
         l1 = nodeList,
         l2 = sh:ppath(s, q)) {
        for (o in l1) {
            let (suc = ! xt:member(o, l2)) {
                sh:report(sh:disjoint, sh, s, p, o, suc, vis) ;
                set (res = res && suc)
            }
        } ;
        return (res)
    }
}



function sh:equals (sh, vis, s, p, q, nodeList) {
    let (l1 = nodeList, 
         l2 = sh:ppath(s, q)) {         
         let (b1 = sh:myequals (sh, vis, s, p, q, l1, l2),
              b2 = sh:myequals (sh, vis, s, p, q, l2, l1)) {
        return (b1 && b2)
        }
    }
}

function sh:myequals (sh, vis, s, p, q, l1, l2) {
    let (res = true) {
        for (o in l1) {
            let (suc = xt:member(o, l2)) {
                 sh:report(sh:equals, sh, s, p, o, suc, vis) ;
                 set(res = res && suc) 
            }
        } ;
        return (res)
    }
}




#
# Boolean operators 
#

function sh:not (sh, vis, s, p, cst, nodeList) {
    let (res = true) {
        for (o in nodeList) {
            let (suc = ! sh:core(sh:shaclGraph(),cst, false, xt:list(o))) { 
                sh:report(sh:not, sh, s, p, o, suc, vis);
                set (res = res && suc)
            }
        } ;
        return (res)
    }
}

#
# basic sh:and sh:or sh:xone defined in core.rq
# cst is the bnode start list of shapes of boolean shape sh
# sh sh:and cst where cst = (sh1 .. shn)
#
function sh:and (sh, vis, s, p, cst, nodeList) {
    let (res = true, shList = sh:getShapeConstraint(sh:pathboolean,  cst)) {
        for (o in nodeList) {
            let (suc = coalesce(sh:and(sh:shaclGraph(),shList, o), false)) { 
                if (suc, true, sh:report(sh:and, sh, s, p, o, suc, vis));
                set (res = res && suc)
            }
        };
        return (res)
    }
}

function sh:or (sh, vis, s, p, cst, nodeList) {
    let (res = true, shList = sh:getShapeConstraint(sh:pathboolean, cst)) {
        for (o in nodeList) {
            let (suc = coalesce(sh:or(sh:shaclGraph(),shList, o), false)) { 
                if (suc, true, sh:report(sh:or, sh, s, p, o, suc, vis));
                set (res = res && suc)
            }
        };
        return (res)
    }
}

function sh:xone (sh, vis, s, p, cst, nodeList) {
    let (res = true, shList = sh:getShapeConstraint(sh:pathboolean,  cst)) {
        for (o in nodeList) {
            let (suc = coalesce(sh:xone(sh:shaclGraph(),shList, o), false)) { 
                if (suc, true, sh:report(sh:xone, sh, s, p, o, suc, vis));
                set (res = res && suc)
            }
        };
        return (res)
    }
}




#
# cst is the bnode start of the list of shapes of the boolean operator
# and (sh1 .. shn) ; cst = (sh1 .. shn)
# Return the list of shapes of the boolean operator
# boolean prop = true if the constraint is a sh:property shape
#
function sh:pathboolean(shape, cst) {
     let ( 
        SELECT ?cst  ?shape (aggregate(?sh) as ?list)
		WHERE {
            graph ?shape { 
                ?cst rdf:rest*/rdf:first ?sh
            } } ) { 
            return (list)
        }
}













